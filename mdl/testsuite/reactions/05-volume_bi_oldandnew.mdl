/****************************************************************************
 * Test 05: Bimolecular volume rxn time rate of collision
 *
 *    This file was assembled to compare the rate of collisions detected
 *    between the traditional and trimolecular code pathways.  It uses the same
 *    setup as the last several tests, but very large reaction rates.
 *
 *    Accurate 3D reactions are turned on
 *
 * Author: Jed Wing <jed@salk.edu>
 * Date:   2008-04-04
 ****************************************************************************/

basename = "05-volume_bi_oldandnew"
countdir = "dat/" & basename & "/"
vizdir   = "./viz_dat/" & basename & "/"
sprintf(seed, "%04g", SEED)
cpfile   = basename & seed & ".cp"

iterations = 100
timestep = 1e-6
boxdims = 0.25
voldiffconst = 2.0e-6

/* ACCURATE_3D_REACTIONS = FALSE */
TIME_STEP_MAX = 1.0
CHECKPOINT_ITERATIONS = 9999999999
CHECKPOINT_OUTFILE = cpfile

PARTITION_X = [ [ -0.3 TO 0.3 STEP 0.015 ] ]
PARTITION_Y = [ [ -0.3 TO 0.3 STEP 0.015 ] ]
PARTITION_Z = [ [ -0.3 TO 0.3 STEP 0.015 ] ]

ITERATIONS = iterations
TIME_STEP = timestep

INCLUDE_FILE = "volmols.mdl"

DEFINE_REACTIONS {
  V_t1 + V_t2        -> V_t1 + I_t1 [3.0e20]
  V_b  + V_t1 + V_a  -> I_t1 [3.0e10]
  V_b  + V_t2 + V_a  -> I_t1 [3.0e10]

  V_t3 + V_t4        -> V_t3 + I_t2  [3.0e20]
}

main_box BOX 
{
    CORNERS = [-boxdims, -boxdims, -boxdims], [boxdims, boxdims, boxdims]
}

outer_box BOX
{
    CORNERS = [-4,-4,-4], [4,4,4] 
}

INSTANTIATE world OBJECT 
{
    main_box OBJECT main_box {} 
    outer_box OBJECT outer_box{}  
    release_site_4 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t1
        NUMBER_TO_RELEASE = 1000
    }
    release_site_5 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t2
        NUMBER_TO_RELEASE = 1000
    }
    release_site_6 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t3
        NUMBER_TO_RELEASE = 1000
    }
    release_site_7 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t4
        NUMBER_TO_RELEASE = 1000
    }
}

REACTION_DATA_OUTPUT {
  STEP = timestep
  {COUNT[V_t1,world.main_box]}    => countdir & "V_t1." & seed & ".dat"
  {COUNT[V_t2,world.main_box]}    => countdir & "V_t2." & seed & ".dat"
  {COUNT[V_t3,world.main_box]}    => countdir & "V_t3." & seed & ".dat"
  {COUNT[V_t4,world.main_box]}    => countdir & "V_t4." & seed & ".dat"
}

INCLUDE_FILE = "vizoutput.mdl"
