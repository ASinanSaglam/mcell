/****************************************************************************
 * Test 02: All volume rxn types except for macromolecular.
 *
 *    An untargetted test aiming to flush out any gross errors introduced into
 *    the reaction machinery.  No numeric results were generated to compare the
 *    results against, so we're mainly looking for crashes or other obvious
 *    defects.  This file was mainly produced as a comparison against
 *    01-volume.mdl, to be run on the pre-macromolecular versions of MCell.
 *
 * Author: Jed Wing <jed@salk.edu>
 * Date:   2008-04-04
 ****************************************************************************/

basename = "02-volume_nmacro"
countdir = "dat/" & basename & "/"
vizdir   = "./viz_dat/" & basename & "/"
sprintf(seed, "%04g", SEED)

iterations = 100
timestep = 1e-6
boxdims = 1.0
voldiffconst = 2.0e-6

ITERATIONS = iterations
TIME_STEP = timestep

DEFINE_MOLECULES
{
    V_i {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_u {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_b1 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_b2 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t1 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t2 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t3 {DIFFUSION_CONSTANT_3D = voldiffconst}

    I_u {DIFFUSION_CONSTANT_3D = 0}
    I_b {DIFFUSION_CONSTANT_3D = 0}
    I_t {DIFFUSION_CONSTANT_3D = 0}
}

DEFINE_REACTIONS {
  V_u -> I_u [3.0e2]
  V_b1 + V_b2 -> I_b [6.0e8]
  V_t1 + V_t2 + V_t3 -> I_t [3.0e12]
}

all_release_sites OBJECT {
    release_site_1 SPHERICAL_RELEASE_SITE {
        LOCATION = [-boxdims/2, -boxdims/2, -boxdims/2]
        MOLECULE = V_u
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_2 SPHERICAL_RELEASE_SITE {
        LOCATION = [-boxdims/2, -boxdims/2,  boxdims/2]
        MOLECULE = V_b1
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_3 SPHERICAL_RELEASE_SITE {
        LOCATION = [-boxdims/2,  boxdims/2, -boxdims/2]
        MOLECULE = V_b2
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_4 SPHERICAL_RELEASE_SITE {
        LOCATION = [-boxdims/2,  boxdims/2,  boxdims/2]
        MOLECULE = V_t1
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_5 SPHERICAL_RELEASE_SITE {
        LOCATION = [ boxdims/2, -boxdims/2, -boxdims/2]
        MOLECULE = V_t2
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_6 SPHERICAL_RELEASE_SITE {
        LOCATION = [ boxdims/2, -boxdims/2,  boxdims/2]
        MOLECULE = V_t3
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
    release_site_7 SPHERICAL_RELEASE_SITE {
        LOCATION = [ boxdims/2,  boxdims/2, -boxdims/2]
        MOLECULE = V_i
        NUMBER_TO_RELEASE = 1000
        SITE_DIAMETER = 0
    }
}

main_box BOX
{
    CORNERS = [-boxdims, -boxdims, -boxdims], [boxdims, boxdims, boxdims]
}

outer_box BOX
{
    CORNERS = [-4,-4,-4], [4,4,4]
}

INSTANTIATE world OBJECT
{
    main_box OBJECT main_box {}
    outer_box OBJECT outer_box{}
    release_sites OBJECT all_release_sites { }
}

REACTION_DATA_OUTPUT {
  STEP = timestep
  {COUNT[V_i,world.main_box]}     => countdir & "V_i."  & seed & ".dat"
  {COUNT[V_u,world.main_box]}     => countdir & "V_u."  & seed & ".dat"
  {COUNT[V_b1,world.main_box]}    => countdir & "V_b1." & seed & ".dat"
  {COUNT[V_b2,world.main_box]}    => countdir & "V_b2." & seed & ".dat"
  {COUNT[V_t1,world.main_box]}    => countdir & "V_t1." & seed & ".dat"
  {COUNT[V_t2,world.main_box]}    => countdir & "V_t2." & seed & ".dat"
  {COUNT[V_t3,world.main_box]}    => countdir & "V_t3." & seed & ".dat"
}

VIZ_OUTPUT {
  MODE = DREAMM_V3_GROUPED
  FILENAME = vizdir & "world." & seed
  MOLECULES {
    NAME_LIST { ALL_MOLECULES }
    ITERATION_NUMBERS { ALL_DATA @ [[0 TO 100000000 STEP 100]] }
  }
  MESHES {
    NAME_LIST { ALL_MESHES }
    ITERATION_NUMBERS { ALL_DATA @ [0] }
  }
}
