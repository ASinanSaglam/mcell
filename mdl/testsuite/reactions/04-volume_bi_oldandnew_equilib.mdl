/****************************************************************************
 * Test 04: Bimolecular volume rxn equilibrium
 *
 *    The reaction rates in this file have been computed to give an equilibrium
 *    very close to 50% -- that is, the number of V_t2, I_t1, V_t3, and I_t2
 *    should hover around 500, and when averaged over a long runtime should
 *    give 500 to high accuracy.  V_t1/V_t2 go through the trimolecular code
 *    path, and V_t3/V_t4 go through the traditional code path.
 *
 *    Accurate 3D reactions are turned on
 *
 * Author: Jed Wing <jed@salk.edu>
 * Date:   2008-04-04
 ****************************************************************************/

basename = "04-volume_bi_oldandnew_equilib"
countdir = "dat/" & basename & "/"
vizdir   = "./viz_dat/" & basename & "/"
sprintf(seed, "%04g", SEED)
cpfile   = basename & seed & ".cp"

iterations = 100
timestep = 1e-6
boxdims = 0.25
cx_max_rate = 6.0e8
cx_rate_1 = 3.0e8
cx_rate_2 = 1.5e8
cx_normal_rate = 7.5e7
voldiffconst = 2.0e-6

/* ACCURATE_3D_REACTIONS = FALSE */
TIME_STEP_MAX = 1.0
CHECKPOINT_ITERATIONS = 9999999999
CHECKPOINT_OUTFILE = cpfile

PARTITION_X = [ [ -0.3 TO 0.3 STEP 0.015 ] ]
PARTITION_Y = [ [ -0.3 TO 0.3 STEP 0.015 ] ]
PARTITION_Z = [ [ -0.3 TO 0.3 STEP 0.015 ] ]

ITERATIONS = iterations
TIME_STEP = timestep

DEFINE_MOLECULES
{
    V_t1 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t2 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t3 {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_t4 {DIFFUSION_CONSTANT_3D = voldiffconst}

    /* Never released -- used to trick code into using trimolecular pathway */
    V_a {DIFFUSION_CONSTANT_3D = voldiffconst}
    V_b {DIFFUSION_CONSTANT_3D = voldiffconst}

    I_t1 {DIFFUSION_CONSTANT_3D = 0}  
    I_t2 {DIFFUSION_CONSTANT_3D = 0}  
}

DEFINE_REACTIONS {
  V_t1 + V_t2        -> V_t1 + I_t1 [3.0e7]
  I_t1               -> V_t2 [398.5387]
  V_b  + V_t2 + V_a  -> I_t1 [3.0e10]

  V_t3 + V_t4        -> V_t3 + I_t2  [3.0e7]
  I_t2               -> V_t4 [398.5387]
}

main_box BOX 
{
    CORNERS = [-boxdims, -boxdims, -boxdims], [boxdims, boxdims, boxdims]
}

outer_box BOX
{
    CORNERS = [-4,-4,-4], [4,4,4] 
}

INSTANTIATE world OBJECT 
{
    main_box OBJECT main_box {} 
    outer_box OBJECT outer_box{}  
    release_site_4 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t1
        NUMBER_TO_RELEASE = 1000
    }
    release_site_5 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t2
        NUMBER_TO_RELEASE = 1000
    }
    release_site_6 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t3
        NUMBER_TO_RELEASE = 1000
    }
    release_site_7 RELEASE_SITE {
        SHAPE = world.main_box
        MOLECULE = V_t4
        NUMBER_TO_RELEASE = 1000
    }
}

REACTION_DATA_OUTPUT {
  STEP = timestep
  {COUNT[V_t1,world.main_box]}    => countdir & "V_t1." & seed & ".dat"
  {COUNT[V_t2,world.main_box]}    => countdir & "V_t2." & seed & ".dat"
  {COUNT[V_t3,world.main_box]}    => countdir & "V_t3." & seed & ".dat"
  {COUNT[V_t4,world.main_box]}    => countdir & "V_t4." & seed & ".dat"
}

VIZ_OUTPUT {
  MODE = DREAMM_V3_GROUPED
  FILENAME = vizdir & "world." & seed
  MOLECULES {
    NAME_LIST { ALL_MOLECULES }
    ITERATION_NUMBERS { ALL_DATA @ [[0 TO 100000000 STEP 100]] }
  }
  MESHES {
    NAME_LIST { ALL_MESHES }
    ITERATION_NUMBERS { ALL_DATA @ [0] }
  }
}
